/****************************************
*	Defusing Game Mode					*
*	Author:		steeffeen				*
*	Contact:	mail@steeffeen.com		*
****************************************/

#Extends "Modes/ShootMania/ModeBase.Script.txt"


/****************************************
	INCLUDES
****************************************/

#Include "MathLib" as MathLib
#Include "Libs/Nadeo/Victory.Script.txt" as Victory
#Include "Libs/Nadeo/ShootMania/Score.Script.txt" as Score
#Include "Libs/Nadeo/ShootMania/SM.Script.txt" as SM


/****************************************
	Settings
****************************************/

#Setting	S_RoundsTimeLimit		2.		as _("Time Limit to activate the Bomb (Minutes)")
#Setting	S_RoundsToWin			5		as _("Rounds to win a Map")
#Setting	S_RoundGapToWin			2		as _("Rounds Gap needed to win a Map")
#Setting	S_RoundsLimit			10		as _("Rounds Limit (First Team reaching it wins)")

#Setting	S_ActivationTime		5.		as _("Time to activate the Bomb (Seconds)")
#Setting	S_DefusingTimeFactor	1.5		as _("Factor for Time to defuse the Bomb")
#Setting	S_BombExplosionTime		45.		as _("Time until an activated Bomb explodes (Seconds)")

#Setting	S_EnableRoundRespawn	False	as _("Enable Respawning during Rounds")
#Setting	S_FriendlyFire			False	as _("Friendly-Fire (Team Damage)")

#Setting	S_NbBotsPerTeam			2		as "<hidden>"


/****************************************
	CONSTANTS
****************************************/

#Const	Version					"0.07 (2013-12-15)"
#Const	ScriptName				"Defusing.Script.txt"
#Const	CompatibleMapTypes		"DefusingArena"

#Const	C_BombPickUpDistance	2.5				// Max distance to pick up a dropped bomb


/****************************************
	GLOBALES
****************************************/

declare Ident[][Integer]	G_ClanSpawnIds;		// Ids of each clans spawn blocks
declare Ident[]				G_BombPoleIds;		// Ids of the bomb spots
declare Integer				G_AttackingClan;	// Clan number of the currently attacking team
declare Ident				G_BombCarrierId;	// Id of the object carrying the bomb (player/pole)
declare Vec3				G_BombDropPosition;	// Position of the dropped bomb


/****************************************
	LABELS
****************************************/

***LogVersion***
***
MB_LogVersion(ScriptName, Version);
MB_LogVersion(Score::GetScriptName(), Score::GetScriptVersion());
MB_LogVersion(SM::GetScriptName(), SM::GetScriptVersion());
MB_LogVersion(Victory::GetScriptName(), Victory::GetScriptVersion());
***

***StartServer***
***
// Enable rounds in modebase
MB_UseSectionRound = True;

// Set mode options
UseClans = True;

// UI configuration
SM::SetupDefaultVisibility();
***

***StartMap***
***
// Prepare map
PrepareMap();

// Begin match
ClanScores[1] = 0;
ClanScores[2] = 0;
Score::MatchBegin();
Victory::MatchBegin();
UpdateClanScoresSummary();
***

***StartRound***
***
// Set bots
Users_SetNbFakeUsers(S_NbBotsPerTeam, S_NbBotsPerTeam);

// Prepare next round
SM::UnspawnAllPlayers();
PrepareRound();

// Prepare scores
Score::RoundBegin();
Victory::RoundBegin();
UpdateClanScoresSummary();

MB_Sleep(1000);

// Set the begin of the round
Mode::Synchro_DoBarrier();
StartTime = Now + 3500;
EndTime = MathLib::NearestInteger(StartTime + S_RoundsTimeLimit * 60. * 1000.);
UIManager.UIAll.UISequence = CUIConfig::EUISequence::Playing;

// Announce bomb carrier
if (Players.existskey(G_BombCarrierId)) {
	declare Player <=> Players[G_BombCarrierId];
	UIManager.UIAll.SendChat("Bomb Carrier: "^Player.Login);
}
***

***PlayLoop***
***
// Manage players
foreach (Player in Players) {
	switch (Player.SpawnStatus) {
		case CSmPlayer::ESpawnStatus::NotSpawned: {
		
			if (!S_EnableRoundRespawn && Now > StartTime) {
				// Round has already started
				continue;
			}
			
			if (Player.RequestsSpectate) {
				// The player wants to spectate - Do nothing
				continue;
			}
			
			// Spawn the player
			MySpawnPlayer(Player);
		}
		case CSmPlayer::ESpawnStatus::Spawned: {
			
			if (Player.RequestsSpectate) {
				// The player wants to spectate
				DropBomb(Player);
				UnspawnPlayer(Player);
				continue;
			}
			
			if (Player.RequestedClan != Player.CurrentClan) {
				// The player wants to change its team
				DropBomb(Player);
				UnspawnPlayer(Player);
				continue;
			}
		}
	}
}

// Manage events
foreach (Event in PendingEvents) {
	switch (Event.Type) {
		case CSmModeEvent::EType::OnHit: {
		
			if (Event.Shooter == Event.Victim) {
				// Self hit
				Discard(Event);
				continue;
			}
			
			if (!S_FriendlyFire && Event.Shooter.CurrentClan == Event.Victim.CurrentClan) {
				// Invalid Team Hit
				Discard(Event);
				continue;
			}
			
			// Valid hit
			
			// Calculate points
			declare Points = Event.Damage / 100;
			if (Event.Shooter.CurrentClan == Event.Victim.CurrentClan) {
				// Team Hit
				Points *= -1;
			}
			
			// Grant points for the shooter
			Score::AddPoints(Event.Shooter, Points);
			
			// Set the amount of + displayed for the shooter
			Event.ShooterPoints = Points;
			
			// Pass on event
			PassOn(Event);
		}
		case CSmModeEvent::EType::OnArmorEmpty: {
			// Player has been eliminated
			DropBomb(Event.Victim);
			PassOn(Event);
		}
		case CSmModeEvent::EType::OnPlayerRequestRespawn: {
			// Player wants to be despawned
			DropBomb(Event.Player);
			PassOn(Event);
		}
		case CSmModeEvent::EType::OnCapture: {
			// Bomb has been planted
			G_BombCarrierId = Event.BlockPole.Id;
			EndTime = MathLib::NearestInteger(Now + S_BombExplosionTime * 1000.);
			PassOn(Event);
		}
		default: {
			// Not used events
			Discard(Event);
		}
	}
}

// Manage poles
foreach (PoleId in G_BombPoleIds) {
	declare Pole <=> BlockPoles[PoleId];
	
	if (BlockPoles.existskey(G_BombCarrierId)) {
		// Bomb has been activated
		
		if (PoleId != G_BombCarrierId) {
			// On another pole...
			Pole.Gauge.Speed = 0;
			Pole.Gauge.Value = 0;
			continue;
		}
		
		declare Defusing = False;
		
		// Loop through capturing players
		foreach (PlayerId in Pole.Sector.PlayersIds) {
			declare Player <=> Players[PlayerId];
			
			if (Player.CurrentClan == G_AttackingClan) {
				// Attacker
				continue;
			}
			
			// Defender
			Defusing = True;
			break;
		}
		
		if (Defusing) {
			// Being defused!
			if (Pole.Gauge.ValueReal <= 0.) {
				// Completely defused!
				Victory::SetRoundWinnerIfNoWinner(3-G_AttackingClan);
			}
			Pole.Gauge.Speed = -MathLib::NearestInteger(Pole.Gauge.Max / (S_ActivationTime * S_DefusingTimeFactor * 1000.));
			continue;
		}
		
		// Not defusing
		Pole.Gauge.Speed = 0;
		Pole.Gauge.Value = Pole.Gauge.Max;
		continue;
	}
	
	// Bomb is not yet activated
	declare Activating = False;
	
	// Loop through capturing players
	foreach (PlayerId in Pole.Sector.PlayersIds) {
		declare Player <=> Players[PlayerId];
		
		if (Player.CurrentClan != G_AttackingClan) {
			// Defender
			continue;
		}
		
		if (Player.Id != G_BombCarrierId) {
			// Not the bomb carrier
			continue;
		}

		// Attacker
		Activating = True;
	}
	
	if (Activating) {
		// Being activated!
		Pole.Gauge.Speed = MathLib::NearestInteger(Pole.Gauge.Max / (S_ActivationTime * 1000.));
		continue;
	}
	
	// Not activating
	Pole.Gauge.Speed = 0;
	Pole.Gauge.Value = 0;
}

// Manage bomb
if (G_BombCarrierId == NullId) {
	// Bomb is dropped
	foreach (Player in Players) {
		if (Player.CurrentClan != G_AttackingClan) {
			// Not an attacker
			continue;
		}
		if (Player.Armor <= 0) {
			// Eliminated player
			continue;
		}
		declare Distance = MathLib::Distance(Player.Position, G_BombDropPosition);
		if (Distance > C_BombPickUpDistance) {
			// Too far away
			continue;
		}
		PickUpBomb(Player);
	}
}

// Round end conditions
if (Now >= EndTime) {
	if (BlockPoles.existskey(G_BombCarrierId)) {
		// Bomb exploded
		Victory::SetRoundWinnerIfNoWinner(G_AttackingClan);
	} else {
		// Attackers didn't set bomb
		Victory::SetRoundWinnerIfNoWinner(3-G_AttackingClan);
	}
} else {
	foreach (Clan => Team in Teams) {
		if (ClansNbPlayers[Clan+1] <= 0 || ClansNbPlayersAlive[Clan+1] > 0) {
			// No players in the team or not all of them eliminated yet
			continue;
		}
		// Whole team has been eliminated
		if (Clan+1 == G_AttackingClan && BlockPoles.existskey(G_BombCarrierId)) {
			// Bomb is already planted, Defense still has to defuse
			continue;
		}
		Victory::SetRoundWinnerIfNoWinner(2-Clan);
		break;
	}
}
if (Victory::NoRoundWinner()) continue;
// Round is won
MB_StopRound = True;
***

***EndRound***
***
// End round
StartTime = -1;
EndTime = -1;
Score::RoundEnd();
Victory::SetRoundDrawIfNoWinner();
UIManager.UIAll.UISequence = CUIConfig::EUISequence::EndRound;

// Validate round winner
if (!Victory::IsRoundDraw()) {
	foreach (Index => Team in Teams) {
		if (!Victory::IsRoundWinner(Index+1)) continue;
		ClanScores[Index+1] += 1;
		break;
	}
	UpdateClanScoresSummary();
}
Victory::RoundEnd();

MB_Sleep(1000);

// Match end conditions
Victory::SetMatchWinnerFromScore(S_RoundsToWin, S_RoundGapToWin, S_RoundsLimit);
if (Victory::NoMatchWinner()) continue;
// Match is won
MB_StopMap = True;
***

***EndMap***
***
// End match
Score::MatchEnd();
Victory::MatchEnd();
***


/****************************************
	FUNCTIONS
****************************************/

// Drop the bomb on the player's position if he's carrying it
Void DropBomb(CSmPlayer _Player) {
	if (_Player.Id != G_BombCarrierId) {
		// Not the bomb carrier
		return;
	}
	// Drop the bomb
	G_BombCarrierId = NullId;
	G_BombDropPosition = _Player.Position;
}

// Pick up a dropped bomb
Void PickUpBomb(CSmPlayer _Player) {
	if (G_BombCarrierId != NullId) {
		// Bomb isn't dropped
		return;
	}
	// Pick it up
	G_BombCarrierId = _Player.Id;
}

// Spawn the given player
Void MySpawnPlayer(CSmPlayer _Player) {
	// Find a spawn block for the player
	declare SpawnIds = G_ClanSpawnIds[_Player.RequestedClan];
	declare SpawnId = SpawnIds[MathLib::Rand(0, SpawnIds.count-1)];
	declare Spawn <=> BlockSpawns[SpawnId];
	
	// Spawn the player in its desired team
	SM::SpawnPlayer(_Player, _Player.RequestedClan, Spawn, StartTime);
}

// Load and prepare map for the match
Void PrepareMap() {
	// Turn lights on
	foreach (Base in Bases) {
		Base.IsActive = True;
		Base.Clan = 0;
	}
	
	// Save team spawns
	G_ClanSpawnIds.clear();
	foreach (Spawn in BlockSpawns) {
		if (Spawn.Order != 1 && Spawn.Order != 2) {
			// No team spawn
			continue;
		}
		
		if (!G_ClanSpawnIds.existskey(Spawn.Order)) {
			// Init spawns array
			G_ClanSpawnIds[Spawn.Order] = Ident[];
		}
		
		// Save spawn id in array of its order
		G_ClanSpawnIds[Spawn.Order].add(Spawn.Id);
	}
	
	// Save bomb spots
	G_BombPoleIds.clear();
	foreach (Pole in BlockPoles) {
		if (Pole.Tag != "A" && Pole.Tag != "B" && Pole.Tag != "C") {
			// No bomb spot
			continue;
		}
		
		// Init values
		Pole.Gauge.Max = 100000;
		
		// Add pole to bomb spots
		G_BombPoleIds.add(Pole.Id);
	}
	
	// Reset attacking team
	G_AttackingClan = -1;
	
	// Reset bomb carrying times
	foreach (Score in Scores) {
		declare Integer LastCarriedBomb for Score;
		LastCarriedBomb = -1;
	}
}

// Prepare next round
Void PrepareRound() {
	// Despawn all players
	SM::UnspawnAllPlayers();
	
	// Switch sides if necessary
	declare SwitchSides = True;
	if (G_AttackingClan <= 0) {
		// Pick randomly which team starts attacking first
		G_AttackingClan = MathLib::Rand(1, 2);
		if (G_AttackingClan == 1) {
			// The spawns are fine like that - No need to switch sides
			SwitchSides = False;
		}
	} else {
		// Switch attacking team
		G_AttackingClan = 3 - G_AttackingClan;
	}
	if (SwitchSides) {
		// The teams are switching sides! - Switch spawn arrays
		declare Temp = G_ClanSpawnIds[1];
		G_ClanSpawnIds[1] = G_ClanSpawnIds[2];
		G_ClanSpawnIds[2] = Temp;
	}
	
	// Prepare spawns
	foreach (Order => SpawnIds in G_ClanSpawnIds) {
		foreach (SpawnId in SpawnIds) {
			declare Spawn <=> BlockSpawns[SpawnId];
			
			// Set team color
			Spawn.Base.Clan = Order;
		}
	}
	
	// Prepare poles
	foreach (PoleId in G_BombPoleIds) {
		declare Pole <=> BlockPoles[PoleId];
		
		// Set team color
		Pole.Base.Clan = G_AttackingClan;
		Pole.Gauge.Clan = G_AttackingClan;
		
		// Reset capture progress
		Pole.Gauge.Speed = 0;
		Pole.Gauge.Value = 0;
	}
	
	// Find and assign bomb carrier for the next round
	declare LastCarried = -1;
	G_BombCarrierId = NullId;
	foreach (Player in Players) {
		if (Player.RequestedClan != G_AttackingClan) {
			// Wrong team
			continue;
		}
		if (Player.Score == Null) {
			// Player that can't carry bombs (Bot)
			continue;
		}
		
		declare LastCarriedBomb for Player.Score = -1;
		if (LastCarriedBomb < 0) {
			// Player didn't carry the bomb so far - Directly choose him
			G_BombCarrierId = Player.Id;
			break;
		}
		
		if (G_BombCarrierId == NullId) {
			// No player found so far - Queue him
			G_BombCarrierId = Player.Id;
			LastCarried = LastCarriedBomb;
			continue;
		}
		
		if (LastCarried <= LastCarriedBomb) {
			// Another queued player is waiting longer than this one
			continue;
		}
		
		// Queue player
		G_BombCarrierId = Player.Id;
		LastCarried = LastCarriedBomb;
	}
	
	// Set bomb carry time for chosen player
	if (Players.existskey(G_BombCarrierId)) {
		declare Integer LastCarriedBomb for Players[G_BombCarrierId].Score;
		LastCarriedBomb = Now;
	} else {
		// No player available - Drop the bomb in an attack spawn
		declare SpawnIds = G_ClanSpawnIds[G_AttackingClan];
		declare SpawnId = SpawnIds[MathLib::Rand(0, SpawnIds.count-1)];
		declare Spawn <=> BlockSpawns[SpawnId];
		G_BombDropPosition = Spawn.Position;
	}
}

// Updates clan scores at the top
Void UpdateClanScoresSummary() {
	// Determine players to use for team colors
	declare PlayerClan1Id = NullId;
	declare PlayerClan2Id = NullId;
	foreach (Player in Players) {
		if (Player.CurrentClan == 1) {
			if (PlayerClan1Id != NullId) continue;
			PlayerClan1Id = Player.Id;
		}
		if (Player.CurrentClan == 2) {
			if (PlayerClan2Id != NullId) continue;
			PlayerClan2Id = Player.Id;
		}
		if (PlayerClan1Id != NullId && PlayerClan2Id != NullId) break;
	}
	
	if (PlayerClan1Id == NullId || PlayerClan2Id == NullId) {
		// No sufficient players found for each team - Disable overlay
		UIManager.UIAll.OverlayScoreSummary = False;
		return;
	}
	
	// Enable overlay and set data
	UIManager.UIAll.OverlayScoreSummary = True;
	UIManager.UIAll.ScoreSummary_Player1 = PlayerClan1Id;
	UIManager.UIAll.ScoreSummary_Player2 = PlayerClan2Id;
	UIManager.UIAll.ScoreSummary_Points1 = ClanScores[1];
	UIManager.UIAll.ScoreSummary_Points2 = ClanScores[2];
	UIManager.UIAll.ScoreSummary_MatchPoints1 = -1;
	UIManager.UIAll.ScoreSummary_MatchPoints2 = -1;
}
